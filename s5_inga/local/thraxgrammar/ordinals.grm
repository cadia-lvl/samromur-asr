# Expand ordinals
#
# Copyright 2017  Reykjavik University (Author: Inga Rún Helgadóttir)
# Apache 2.0

import 'local/thraxgrammar/common.grm' as c;
import 'local/thraxgrammar/numbers.grm' as n;

insspace = "".utf8 : " ".utf8;

delzero = "0".utf8 : "".utf8;
delone  = "1".utf8 : "".utf8;
delperiod = ".".utf8 : "".utf8;
insand = "".utf8 : " og ".utf8;

ordUnits = Optimize[StringFile['local/thraxgrammar/lex/ordinals_units_lexicon.txt',
'utf8', 'utf8']];

ordTeens = Optimize[StringFile['local/thraxgrammar/lex/ordinals_teens_lexicon.txt', 'utf8', 'utf8']];

ordDecades = Optimize[StringFile['local/thraxgrammar/lex/ordinals_decades_lexicon.txt', 'utf8', 'utf8']];

ordinals_20_to_99 = ordDecades delzero delperiod | (ordDecades insand
ordUnits <-3>);

hundreds1 = ((((("eitt".utf8 <2.0>) insspace) | delone)
  ("".utf8 : ("hundraðasta".utf8 | "hundraðasti".utf8)))
  | (n.neutral_units2to9 insspace
  ("".utf8 : ("hundraðasti".utf8 | "hundraðasta".utf8 | "hundruðustu".utf8))));

hundreds2 = ((((("eitt".utf8 <2.0>) insspace) | delone)
  ("".utf8 : "hundrað".utf8))
  | (n.neutral_units2to9 insspace
  ("".utf8 : "hundruð".utf8)));

ordinals_100_to_999 = Optimize[
  hundreds1 delzero delzero delperiod |
  (hundreds2 delzero insand ordUnits <-1>) |
  (hundreds2 insand ordTeens <-2>) |
  (hundreds2 (insspace | insand) ordinals_20_to_99 <-10>)]
;

ordinals = Optimize[
  (ordUnits <-0.5>) |
  (ordTeens <-1>) |
  (ordinals_20_to_99 <-2>) |
  (ordinals_100_to_999 <-10>)]
;

#export ORDINALNAMES = Optimize[ordinals @ n.add_and];
export ORDINALNAMES = ordinals;

# Avoid rewriting to 1., 2. and 10., since not always appropriate.
# f.ex. "fyrstu tölur sýna", "hver annar gæti það verið?", "mig langar að tíunda þau hér"
ordUnits3to9 = Optimize[StringFile['local/thraxgrammar/lex/ordinals_units_3-9.txt',
'utf8', 'utf8']];
ord11_to_19 = Optimize[StringFile['local/thraxgrammar/lex/ordinals_11-19.txt', 'utf8', 'utf8']];

# export ORDINALNAMES_to_abbreviate = Optimize[
#   (ordUnits3to9 <-0.5>) |
#   (ord11_to_19 <-1>) |
#   (ordinals_20_to_99 <-2>) |
#   (ordinals_100_to_999 <-10>)]
# ;

# In most cases they want the first ten ordinals written out
export ORDINALNAMES_to_abbreviate = Optimize[
  (ord11_to_19 <-1>) |
  (ordinals_20_to_99 <-2>) |
  (ordinals_100_to_999 <-10>)]
;